#include "sdl/sdl.ceu"

/* INITIALIZATION */
/* FINALIZATION */

var& _SDL_Window window =
    &_SDL_CreateWindow("Input",
                       _SDL_WINDOWPOS_CENTERED, _SDL_WINDOWPOS_CENTERED,
                       640, 480,
                       _SDL_WINDOW_SHOWN)
        finalize (window) with
            _SDL_DestroyWindow(&&window);
        end;

var& _SDL_Renderer renderer =
    &_SDL_CreateRenderer(&&window, -1, 0)
        finalize (renderer) with
            _SDL_DestroyRenderer(&&renderer);
        end;

/* EXECUTION */

var SDL_Rect r = val SDL_Rect(200,200, 50,50);

par/or do
    await SDL_QUIT;
with
    var _SDL_KeyboardEvent&& key;
    every key in SDL_KEYDOWN do
        if key:keysym.sym == _SDLK_UP then
            r.y = r.y - 10;
        else/if key:keysym.sym == _SDLK_DOWN then
            r.y = r.y + 10;
        else/if key:keysym.sym == _SDLK_LEFT then
            r.x = r.x - 10;
        else/if key:keysym.sym == _SDLK_RIGHT then
            r.x = r.x + 10;
        end
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&&renderer, 0xFF,0xFF,0xFF,0x00);
        _SDL_RenderFillRect(&&renderer, null);
        _SDL_SetRenderDrawColor(&&renderer, 0x00,0x00,0xFF,0x00);
        _SDL_RenderFillRect(&&renderer, (&&r as _SDL_Rect&&));
        _SDL_RenderPresent(&&renderer);
    end
end

escape 0;

